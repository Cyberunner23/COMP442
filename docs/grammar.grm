Start_ -> Program_  . 

Program_ -> ClassDecls_ FuncDefs_ Main_ LocalScope_ Do_ Statements_ End_  . 

ClassDecls_ -> Class_ Identifier_ OptionalInherits_ OpenCurlyBrace_ MemberDecls_ CloseCurlyBrace_ SemiColon_ ClassDecls_  . 
ClassDecls_ ->  . 

FuncDefs_ -> Identifier_ FuncSigNamespace_ LocalScope_ Do_ Statements_ End_ FuncDefs_  . 
FuncDefs_ ->  . 

MemberDecls_ -> Visibility_ MemberDecl_ MemberDecls_  . 
MemberDecls_ ->  . 

MemberDecl_ -> Identifier_ FuncOrVarDecl_ .
MemberDecl_ -> TypeNoID_ Identifier_ ArrayDims_ SemiColon_  . 

FuncOrVarDecl_ -> FuncDecl_ .
FuncOrVarDecl_ -> Identifier_ ArrayDims_ SemiColon_ .

Visibility_ -> Public_  . 
Visibility_ -> Private_  . 


Statement_ -> If_ OpenBrace_ BoolExpr_ CloseBrace_ Then_ StatementBlock_ Else_ StatementBlock_ SemiColon_  . 
Statement_ -> While_ OpenBrace_ BoolExpr_ CloseBrace_ StatementBlock_ SemiColon_  . 
Statement_ -> Read_ OpenBrace_ StatementVar_ CloseBrace_ SemiColon_  . 
Statement_ -> Write_ OpenBrace_ Expression_ CloseBrace_ SemiColon_  . 
Statement_ -> Return_ OpenBrace_ Expression_ CloseBrace_ SemiColon_  . 
Statement_ -> AssignStatementOrFuncCall_ .

StatementBlock_ -> Do_ Statements_ End_  . 
StatementBlock_ -> Statement_  . 
StatementBlock_ ->  . 

StatementVar_ -> Identifier_ StatementVarOrFuncCall_ . 

StatementVarOrFuncCall_ -> Indices_ StatementVar_ .
StatementVarOrFuncCall_ -> OpenBrace_ FuncCallParams_ CloseBrace_ Period_ StatementVar_ .

StatementVar_ -> Period_ StatementVar_ .
StatementVar_ -> .


AssignStatementOrFuncCall_ -> Identifier_ VarOrFuncCallExt_ .

VarOrFuncCallExt_ -> Indices_ VarExt_ .
VarOrFuncCallExt_ -> OpenBrace_ FuncCallParams_ CloseBrace_ FuncCallExt_ .

VarExt_ -> Equal_ Expression_ SemiColon_ .
VarExt_ -> Period_ AssignStatementOrFuncCall_ .

FuncCallExt_ -> SemiColon_ .
FuncCallExt_ -> Period_ AssignStatementOrFuncCall_ .

FuncParams_ -> Type_ Identifier_ ArrayDims_ FuncParamsRest_  . 
FuncParams_ ->  . 

AddOp_ -> Plus_  . 
AddOp_ -> Minus_  . 
AddOp_ -> Or_  . 

OptionalInherits_ -> Inherits_ Identifier_ InheritedClasses_  . 
OptionalInherits_ ->  . 

BoolExpr_ -> ArithExpr_ CompareOp_ ArithExpr_  . 

FuncDecl_ -> OpenBrace_ FuncParams_ CloseBrace_ Colon_ TypeOrVoid_ SemiColon_  . 

FuncParamRest_ -> Comma_ Expression_ FuncParamRest_  . 
FuncParamRest_ ->  . 

LocalScope_ -> Local_ VarDecls_  . 
LocalScope_ ->  . 

ArrayDims_ -> OpenSquareBrace_ OptionalInt_ CloseSquareBrace_ ArrayDims_  . 
ArrayDims_ ->  . 

Expression_ -> ArithExpr_  BoolExprOrNone_ . 

BoolExprOrNone_ -> CompareOp_ ArithExpr_ .
BoolExprOrNone_ -> .

Statements_ -> Statement_ Statements_  . 
Statements_ ->  . 

ArithExpr_ -> Factor_ RightTerm_ RightArithExpr_  . 

RightArithExpr_ ->  . 
RightArithExpr_ -> AddOp_ Factor_ RightTerm_ RightArithExpr_  . 

FuncSigNamespace_ -> OpenBrace_ FuncParams_ CloseBrace_ Colon_ TypeOrVoid_ .
FuncSigNamespace_ -> ColonColon_ Identifier_ OpenBrace_ FuncParams_ CloseBrace_ Colon_ TypeOrVoid_ .

FuncParamsRest_ -> Comma_ Type_ Identifier_ ArrayDims_ FuncParamsRest_  . 
FuncParamsRest_ ->  . 

InheritedClasses_ -> Comma_ Identifier_ InheritedClasses_  . 
InheritedClasses_ ->  . 

Sign_ -> Plus_  . 
Sign_ -> Minus_  . 

CompareOp_ -> EqualEqual_  . 
CompareOp_ -> LesserGreater_  . 
CompareOp_ -> Lesser_  . 
CompareOp_ -> Greater_  . 
CompareOp_ -> LesserEqual_  . 
CompareOp_ -> GreaterEqual_  . 

VarDecls_ -> Identifier_ ArrayDims_ SemiColon_ VarDecls_  . 
VarDecls_ ->  . 

Factor_ -> Identifier_ VarOrFuncCall_  . 
Factor_ -> IntNum_  . 
Factor_ -> FloatNum_  . 
Factor_ -> OpenBrace_ ArithExpr_ CloseBrace_  . 
Factor_ -> Not_ Factor_  . 
Factor_ -> Sign_ Factor_  . 

VarOrFuncCall_ -> Indices_ FactorVar_ .
VarOrFuncCall_ -> OpenBrace_ FuncCallParams_ CloseBrace_ FactorFuncCall_ .

Indices_ -> OpenSquareBrace_ ArithExpr_ CloseSquareBrace_ Indices_  . 
Indices_ ->  .

FactorVar_ -> Period_ Identifier_ VarOrFuncCall_ .
FactorVar_ -> . 

FactorFuncCall_ -> Period_ Identifier_ VarOrFuncCall_ .
FactorFuncCall_ -> .

MultOp_ -> Asterix_  . 
MultOp_ -> FwdSlash_  . 
MultOp_ -> And_  . 

RightTerm_ ->  . 
RightTerm_ -> MultOp_ Factor_ RightTerm_  . 

TypeOrVoid_ -> Type_ .
TypeOrVoid_ -> Void_ .

Type_ -> TypeNoID_ .
Type_ -> Identifier_  . 

TypeNoID_ -> Integer_ .
TypeNoID_ -> Float_ .

OptionalInt_ -> IntNum_ .
OptionalInt_ -> .

FuncCallParams_ -> Expression_ FuncParamRest_  . 
FuncCallParams_ ->  . 

IntNum_ -> intNum .
FloatNum_ -> floatNum .
Integer_ -> integer .
Float_ -> float .
Identifier_ -> identifier .
Void_ -> void .
And_ -> and .
FwdSlash_ -> fwdSlash .
Asterix_ -> asterix .
Period_ -> period .
OpenSquareBrace_ -> openSquareBrace .
CloseSquareBrace_ -> closeSquareBrace .
OpenBrace_ -> openBrace .
CloseBrace_ -> closeBrace .
Not_ -> not .
Plus_ -> plus .
Minus_ -> minus .
SemiColon_ -> semiColon .
Equal_ -> equal . 
LesserGreater_ -> lesserGreater . 
Lesser_ -> lesser . 
Greater_ -> greater . 
LesserEqual_ -> lesserEqual . 
GreaterEqual_ -> greaterEqual . 
Comma_ -> comma .
ColonColon_ -> colonColon .
Colon_ -> colon .
Local_ -> local .
Inherits_ -> inherits .
Or_ -> or .
Do_ -> do .
End_ -> end .
Public_ -> public .
Private_ -> private .
Main_ -> main .
If_ -> if .
Then_ -> then .
Else_ -> else .
While_ -> while .
Read_ -> read .
Write_ -> write .
Return_ -> return .
Class_ -> class .
OpenCurlyBrace_ -> openCurlyBrace .
CloseCurlyBrace_ -> closeCurlyBrace .
EqualEqual_ -> equalEqual .







